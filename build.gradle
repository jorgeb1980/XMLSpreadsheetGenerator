plugins {
    id 'java'
    id 'jacoco'
}
repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.release = 21
}
version = '2.1.0'
group = 'xml.spreadsheet'

jar {
    manifest {
        attributes 'Implementation-Title': 'XML Spreadsheet Generator', 'Implementation-Version': version
    }
}

dependencies {
    // Apparently all these are necessary for running junit 5 in latest versions of gradle ¯\_(ツ)_/¯
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.jdom:jdom2:2.0.6.1'
    testImplementation 'jaxen:jaxen:2.0.0'
}

// tests and coverage configuration
jacoco {
    toolVersion = "0.8.11"
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT"
        showStandardStreams = true
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.implementation
}
