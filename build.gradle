apply plugin: 'java'
// Integration with eclipse IDE (optional; should and must work if you are brave
// and bold enough to edit with vim)
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '1.1'
group = 'xml.spreadsheet'
jar.archiveName = "XMLSpreadsheetGenerator.jar"

jar {
    manifest {
        attributes 'Implementation-Title': 'XML Spreadsheet Generator', 'Implementation-Version': version
    }
}

dependencies {
	compile 'org.apache.velocity:velocity:1.6.2'
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile 'jdom:jdom:1.0'
	testCompile 'jaxen:jaxen:1.1.6'
}

// Jacoco configuration

jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

/**
 * Call this task in order to generate the docs/javadoc folder of the project
 */
task generateJavadocFolder(dependsOn:'build')<< {
	File srcFolder = new File(project.projectDir.getCanonicalPath() + "/src/main/java")
	File docsFolder = new File(project.projectDir.getCanonicalPath() + "/docs/javadoc")
	ant.javadoc(classpath: configurations.compile.asPath, sourcepath: srcFolder, destdir: docsFolder)
}


